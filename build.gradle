apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
   mavenCentral()
   mavenRepo urls: 'http://openjena.org/repo'
}

version = '1.0'

ideaProject {
    javaVersion = '1.6'
}

dependencies {
	compile 'com.hp.hpl.jena:jena:2.6.4'
	compile 'com.hp.hpl.jena:tdb:0.8.10'

    groovy      'org.codehaus.groovy:groovy-all:1.7.4'

	testCompile 'org.spockframework:spock-core:0.4-groovy-1.7'
    testCompile 'junit:junit:4.8.1'
    testCompile 'cglib:cglib-nodep:2.2'
}

task(run, dependsOn: 'classes', type: JavaExec) {
main = 'com.controlj.jenaexp.Main'
classpath = sourceSets.main.runtimeClasspath
//args = Arrays.asList(props.split()) 
}

// The following strangeness is to get groovy dependencies only in the test configurations
configurations {
    compile.extendsFrom = [ providedCompile ]
    testCompile.extendsFrom groovy
}


jar {
    from configurations.compile.collect {
      it.isDirectory() ? it : zipTree(it).matching { exclude 'META-INF/MANIFEST.MF' }
    }
    manifest {
    	attributes('Main-Class': 'com.controlj.jenaexp.Main')
    }
}


File launcherBuildDir = file('build')
String scriptName = 'jenaexp.sh'
File script = new File(launcherBuildDir, scriptName)

task launchers(dependsOn: jar) << {
    launcherBuildDir.mkdirs()
    
    script.text = """#!/bin/bash
java -jar libs/${jar.archiveName} \$*
"""
    script.setExecutable(true, false);

    File batch = new File(launcherBuildDir, 'trendclient.bat')
    batch.text = """@echo off
java -jar libs\\${jar.archiveName} %*
"""    
}

